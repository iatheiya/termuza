apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace 'com.termux.shared'
    compileSdk 35
    ndkVersion (System.getenv("JITPACK_NDK_VERSION") ?: "26.1.10909125")

    defaultConfig {
        minSdk 24
        targetSdk 35
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            ndkBuild {
                cppFlags ''
            }
        }
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        ndkBuild {
            path file('src/main/cpp/Android.mk')
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    api libs.androidx.appcompat
    implementation libs.androidx.annotation
    api libs.androidx.core.ktx
    implementation libs.com.google.material
    implementation libs.com.google.guava

    implementation libs.markwon.core
    implementation libs.markwon.ext.strikethrough
    implementation libs.markwon.linkify
    implementation libs.markwon.recycler

    implementation libs.org.lsposed.hiddenapibypass
    implementation libs.androidx.window
    implementation libs.commons.io

    api project(":terminal-view")
    implementation "com.termux:termux-am-library:v2.0.0"

    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso.core

    coreLibraryDesugaring libs.com.android.tools.desugar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.termux'
                artifactId = 'termux-shared'
                version = '0.118.0'
            }
        }
    }
}